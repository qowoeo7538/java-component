buildscript {
    repositories {
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("io.spring.gradle:propdeps-plugin:0.0.8")
    }
}

plugins {
    id "com.gradle.build-scan" version "2.2.1"
    id "io.spring.dependency-management" version "1.0.4.RELEASE" apply false
}

// 审视扫描
buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'

    publishAlways()
}

ext {
    moduleProjects = subprojects.findAll {
        !it.name.equals('component-build-script')
    }
    localRepository = '/home/shaw/.m2/repository'
}

// 所有项目配置
configure(allprojects) { project ->

    apply plugin: "propdeps"
    apply plugin: "java"

    group = "org.lucas"
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

// 模块项目
configure(moduleProjects) { subproject ->

    apply plugin: "pmd"
    apply plugin: "findbugs"
    apply plugin: 'maven'
    apply plugin: 'jdepend'
    apply plugin: "io.spring.dependency-management"

    // 定义本地maven仓库的地址, 执行:gradle clean uploadArchives 发布到本地仓库
    uploadArchives.repositories.mavenDeployer {
        repository(url: uri(localRepository))
        // 组名
        pom.groupId = 'org.lucas'
        // 版本号
        pom.version = '1.0-SNAPSHOT'
    }

    // 属性配置
    ext {
        artifactId = uploadArchives.repositories.mavenDeployer.pom.artifactId

        disruptorVersion = "3.4.2"
        logbackVersion = "1.2.3"
        unit_jupiter_version = "5.5.0"
        mockitoVersion = "2.12.0"
        hamcrestVersion = "1.3"
        springVersion = "5.1.6.RELEASE"
        javassist_version = "3.25.0-GA"
        collectionsVersion = "9.2.0"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework:spring-framework-bom:${springVersion}"
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
        applyMavenExclusions = false
    }

    [compileJava, compileTestJava].each() {
        it.options.encoding = 'UTF-8'
        it.options.fork = true
        // 指定 javac 路径
        it.options.forkOptions.executable = "/usr/local/sbin/jdk/jdk-11.0.2/bin/javac"
    }

    compileTestJava {
        options.compilerArgs += "-parameters"
    }

    dependencies {
        compile("org.eclipse.collections:eclipse-collections-api:${collectionsVersion}")
        compile("org.eclipse.collections:eclipse-collections:${collectionsVersion}")
        testCompile("org.junit.jupiter:junit-jupiter-api:${unit_jupiter_version}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile("org.mockito:mockito-core:${mockitoVersion}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile("org.hamcrest:hamcrest-all:${hamcrestVersion}")
    }

    project.pmd {
        ignoreFailures = false
        ruleSets = [
                'java-basic',
                'java-braces',
                'java-clone',
                'java-codesize',
                'java-comments',
                'java-controversial',
                'java-coupling',
                'java-design',
                'java-empty',
                'java-finalizers',
                'java-imports',
                'java-j2ee',
                'java-javabeans',
                'java-junit',
                'java-logging-jakarta-commons',
                'java-logging-java',
                'java-migrating',
                'java-naming',
                'java-optimizations',
                'java-strictexception',
                'java-strings',
                'java-sunsecure',
                'java-typeresolution',
                'java-unnecessary',
                'java-unusedcode'
        ]
        reportsDir = file("${project.rootDir}/buildScript/reports")
    }

    task pmd(type: Pmd) {
        source = fileTree(dir: "src/main", include: "**/*.java")
        ignoreFailures = true
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    project.findbugs {
        ignoreFailures = false
        excludeFilter = new File("${project.rootDir}/buildScript/findbugs-filter.xml")
        reportsDir = file("${project.rootDir}/buildScript/reports")
        effort = "max"
    }

    findbugsMain {
        it.source = fileTree(dir: "src/main", include: "**/*.java")
        it.reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugsTest {
        it.source = fileTree(dir: "src/test", include: "**/*.java")
        it.reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

configure(rootProject) {
    description = "Java Component"

    configurations.archives.artifacts.clear()
}