pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/plugins-release' }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.2"
    id "io.spring.gradle-enterprise-conventions" version "0.0.2"
}

include 'component-common'
include 'component-thread'

include 'buildScript'

rootProject.name = 'java-component'

rootProject.children.find { it.name == "buildScript" }.name = "component-build-script"

rootProject.children.each { project ->
    project.buildFileName = "${project.name}.gradle"
}

settings.gradle.projectsLoaded {
    gradleEnterprise {
        buildScan {
            if (settings.gradle.rootProject.hasProperty('customJavaHome')) {
                value("Custom JAVA_HOME", settings.gradle.rootProject.getProperty('customJavaHome'))
            }
            if (settings.gradle.rootProject.hasProperty('customJavaSourceVersion')) {
                value("Custom Java Source Version", settings.gradle.rootProject.getProperty('customJavaSourceVersion'))
            }
            File buildDir = settings.gradle.rootProject.getBuildDir()
            buildDir.mkdirs()
            new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
            buildScanPublished { scan ->
                if (buildDir.exists()) {
                    new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
                }
            }
        }
    }
}

